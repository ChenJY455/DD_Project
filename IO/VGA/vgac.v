// é‡‡ç”¨VGAåè®®ï¼Œåœ¨å±å¹•ä¸Šæ˜¾ç¤ºæ£‹ç›?
// DD_final_project
// 2023/12/25
// Wan Zhenjie
//reference:https://guahao31.github.io/2023_DD/final_project/attachment/vgac.v
module vgac(
    input vga_clk, // æ¥å…¥25.175MHzæ—¶é’Ÿï¼Œå¯ä»?25MHzä»£æ›¿
    input clrn, // é‡ç½®ä¿¡å·, ä½ç”µå¹³æœ‰æ•ˆï¼Œå°†æ‰«æä¿¡å·å½’ä½åˆ°(0,0)
    input [11:0] d_in, //12ä½? RGB ä¿¡å·ï¼Œæ ¼å¼ä¸º bbbb_gggg_rrrr
    output reg [8:0] row_addr, 
    output reg [9:0] col_addr,
    output reg [3:0] r,g,b,
    output reg rdn, //åˆ¤æ–­å½“å‰æ‰«æåˆ°çš„åœ°å€æ˜¯å¦ä¸ºæœ‰æ•ˆæ•°æ®ï¼Œä½ç”µå¹³ä¸ºæœ‰æ•ˆ
    output reg hsync, //æ°´å¹³åŒæ­¥ä¿¡å·
    output reg vsync //å‚ç›´åŒæ­¥ä¿¡å·
    );
    //æ°´å¹³è®¡æ•°å™?
    reg [9:0] h_count; // VGA horizontal counter (0-799): pixels
    always @ (posedge vga_clk) begin
        if (!clrn) begin
            h_count <= 10'h0;
        end else if (h_count == 10'd799) begin
            h_count <= 10'h0;
        end else begin 
            h_count <= h_count + 10'h1;
        end
    end
    //å‚ç›´è®¡æ•°å™?
    reg [9:0] v_count; // VGA vertical   counter (0-524): lines
    always @ (posedge vga_clk or negedge clrn) begin
        if (!clrn) begin
            v_count <= 10'h0;
        end else if (h_count == 10'd799) begin
            if (v_count == 10'd524) begin
                v_count <= 10'h0;
            end else begin
                v_count <= v_count + 10'h1;
            end
        end
    end
    //åŒæ­¥ä¿¡å·
    wire  [9:0] row    =  v_count - 10'd35;     // pixel ram row addr
    wire  [9:0] col    =  h_count - 10'd143;    // pixel ram col addr
    wire        h_sync = (h_count > 10'd95);    //  96 -> 799
    wire        v_sync = (v_count > 10'd1);     //   2 -> 524
    wire        read   = (h_count > 10'd142) && // 143 -> 782
                         (h_count < 10'd783) && //        640 pixels
                         (v_count > 10'd34)  && //  35 -> 514
                         (v_count < 10'd515);   //        480 lines
    //vgaä¿¡å·
    always @ (posedge vga_clk) begin
        row_addr <=  row[8:0]; // pixel ram row address
        col_addr <=  col;      // pixel ram col address
        rdn      <= ~read;     // read pixel (active low)
        hsync       <=  h_sync;   // horizontal synchronization
        vsync       <=  v_sync;   // vertical   synchronization
        r        <=  rdn ? 4'h0 : d_in[3:0]; // 3-bit red
        g        <=  rdn ? 4'h0 : d_in[7:4]; // 3-bit green
        b        <=  rdn ? 4'h0 : d_in[11:8]; // 3-bit blue
    end
endmodule